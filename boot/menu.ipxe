#!ipxe

set squash_host images.squashible.com 
iseq ${squashible_goto} 1 && set menu ${squashible_goto} && goto ${squashible_goto} ||

:squashible
menu SQUASHible (http://www.squashible.com)

item --gap Default:
item local ${space} Boot from local hdd
item --gap CentOS Builds
item centos-7-minimal ${space} Minimal (CentOS 7)
item --gap Debian Builds
item debian-jessie-minimal ${space} Minimal (Debian Jessie)
item --gap Fedora Builds
item fedora-23-minimal ${space} Minimal (Fedora 23)
item --gap Ubuntu Builds
item ubuntu-15.10-minimal ${space} Minimal (Ubuntu 15.10)
item --gap Tools:
item shell ${space} iPXE shell

choose menu || goto squashible
echo ${cls}
goto ${menu} ||
goto squashible_exit

:squashible_exit
clear menu
chain menu.ipxe

:centos-7-minimal
goto dracut-boot

:debian-jessie-minimal
goto debian-live-boot

:ubuntu-15.10-minimal
goto ubuntu-dracut-boot

:fedora-23-minimal
goto dracut-boot

:debian-live-boot
imgfree
kernel http://${squash_host}/images/squashible-${menu}/vmlinuz
module http://${squash_host}/images/squashible-${menu}/initrd.img
imgargs vmlinuz boot=live username=live hooks=filesystem debug fetch=http://${squash_host}/images/squashible-${menu}/filesystem.squashfs apparmor=1 security=apparmor console=tty0 console=ttyS0,115200
boot
goto squashible_exit

:ubuntu-dracut-boot
imgfree
kernel http://${squash_host}/images/squashible-${menu}/vmlinuz
module http://${squash_host}/images/squashible-${menu}/initrd.img
imgargs vmlinuz root=live:http://${squash_host}/images/squashible-${menu}/filesystem.squashfs ip=dhcp nomodeset rd.writable.fsimg rd.info enforcing=0 rd.shell
boot
goto squashible_exit

:dracut-boot
imgfree
kernel http://${squash_host}/images/squashible-${menu}/vmlinuz
module http://${squash_host}/images/squashible-${menu}/initrd.img
imgargs vmlinuz root=live:http://${squash_host}/images/squashible-${menu}/rootfs.img ip=dhcp nomodeset rd.writable.fsimg rd.info enforcing=0 rd.shell
boot
goto squashible_exit

:local
echo Booting from local disks ...
exit 0

:shell
echo Type "exit" to return to menu.
set menu main_menu
shell
goto squashible_exit
